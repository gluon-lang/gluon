use crate::itertools::{Either, Itertools};

use crate::base::{
    ast::{self, Alternative, Argument, Array, AstType, Do, Expr, ExprField, Lambda, Literal, Pattern,
    PatternField, SpannedExpr, SpannedIdent, SpannedPattern, TypeBinding, TypedIdent, ValueBinding, ValueBindings},
    kind::{ArcKind, Kind},
    pos::{self, BytePos, HasSpan, Spanned},
    types::{Alias, AliasData, ArcType, ArgType, BuiltinType, Field, Generic, Type, TypeCache},
    metadata::{Attribute, Metadata, Comment},
};

use std::{str::FromStr, mem};

use crate::{ReplLine, Variant, new_ident};
use crate::token::{Token, StringLiteral};
use crate::ordered_float::NotNan;

use crate::{Error, ErrorEnv, FieldExpr, FieldPattern, MutIdentEnv, TempVecs, TempVecStart, Slice};

grammar<'input, 'env, 'ast, Id>(
    input: &'input dyn crate::ParserSource,
    type_cache: &TypeCache<Id, ArcType<Id>>,
    arena: ast::ArenaRef<'_, 'ast, Id>,
    env: MutIdentEnv<'env, Id>,
    errors: ErrorEnv<'env, 'input>,
    temp_vecs: &mut TempVecs<'ast, Id>,
)
    where Id: Clone + AsRef<str> + std::fmt::Debug;

extern {
    type Location = BytePos;
    type Error = Spanned<Error, BytePos>;

    enum Token<'input> {
        "shebang line" => Token::ShebangLine(<&'input str>),
        "identifier" => Token::Identifier(<&'input str>),
        "operator" => Token::Operator(<&'input str>),
        "string literal" => Token::StringLiteral(<StringLiteral<'input>>),
        "char literal" => Token::CharLiteral(<char>),
        "int literal" => Token::IntLiteral(<i64>),
        "byte literal" => Token::ByteLiteral(<u8>),
        "float literal" => Token::FloatLiteral(<NotNan<f64>>),
        "documentation comment" => Token::DocComment(<Comment<&'input str>>),

        "rec" => Token::Rec,
        "else" => Token::Else,
        "forall" => Token::Forall,
        "if" => Token::If,
        "in" => Token::In,
        "let" => Token::Let,
        "do" => Token::Do,
        "seq" => Token::Seq,
        "match" => Token::Match,
        "then" => Token::Then,
        "type" => Token::Type,
        "with" => Token::With,

        "@" => Token::At,
        ":" => Token::Colon,
        "," => Token::Comma,
        "." => Token::Dot,
        ".." => Token::DotDot,
        "=" => Token::Equals,
        "\\" => Token::Lambda,
        "|" => Token::Pipe,
        "->" => Token::RArrow,
        "?" => Token::Question,

        "{" => Token::LBrace,
        "[" => Token::LBracket,
        "(" => Token::LParen,

        "}" => Token::RBrace,
        "]" => Token::RBracket,
        ")" => Token::RParen,

        "#[" => Token::AttributeOpen,

        "block open" => Token::OpenBlock,
        "block close" => Token::CloseBlock,
        "block separator" => Token::Semi,
    }
}

// Utils

SingleComma: () = {
    "," => (),
    RecoverError,
};

Sep<Rule, Separator>: Vec<Rule> =
    <mut rules: (<Rule> Separator)*> <last: Rule?> => {
        rules.extend(last);
        rules
    };

Comma<Rule>: Vec<Rule> = {
    Sep<Rule, SingleComma>
};


#[inline]
SepTemp<Rule, Separator>: TempVecStart<Rule> = {
    <start: Many1Vec<(<Rule> Separator)>?> <last: Rule?> => {
        let start = start.unwrap_or_else(|| temp_vecs.start());
        if let Some(last) = last {
            temp_vecs.select().push(last);
        }
        start
    }
};

#[inline]
CommaTemp<Rule>: TempVecStart<Rule> = {
    SepTemp<Rule, SingleComma>
};

#[inline]
SepSlice1<Rule, Separator>: &'ast mut Slice<Rule> =
    <start: Many1Vec<(<Rule> Separator)>?> <last: Rule> => {
        match start {
            Some(start) => arena.alloc_extend(temp_vecs.drain(start).chain(Some(last))),
            None => arena.alloc_extend(Some(last)),
        }
    };


#[inline]
SepSlice<Rule, Separator>: &'ast mut Slice<Rule> =
    <start: Many1Vec<(<Rule> Separator)>?> <last: Rule?> => {
        match start {
            Some(start) => arena.alloc_extend(temp_vecs.drain(start).chain(last)),
            None => arena.alloc_extend(last),
        }
    };

#[inline]
CommaSlice<Rule>: &'ast mut Slice<Rule> = {
    SepSlice<Rule, SingleComma>
};

Sp<Rule>: Spanned<Rule, BytePos> =
    <l: @L> <rule: Rule> <r: @R> =>
        pos::spanned2(l, r, rule);

IdentStr: &'input str = {
    "identifier" => <>,
    "(" <"operator"> ")" => <>,
};

VecStart<T>: TempVecStart<T> = {
    T => {
        let start = temp_vecs.start();
        temp_vecs.select().push(<>);
        start
    },
};

Many1Rest<T>: () = {
    <T> => temp_vecs.select().push(<>),
    Many1Rest<T> <T> => {
        temp_vecs.select().push(<>);
    }
};

#[inline]
Many1Vec<T>: TempVecStart<T> = {
    <VecStart<T>>,
    <VecStart<T>> Many1Rest<T>,
};

Many<T>: &'ast mut Slice<T> = {
    => &mut [],
    Many1<T>,
};

Many1<T>: &'ast mut Slice<T> = {
    Many1Vec<T> => {
        arena.alloc_extend(temp_vecs.drain(<>))
    }
};

RecoverError: () = {
    <!> => errors.push(<>.error),
};

SkipExtraTokens: () = {
    => (),
    RecoverError,
};

Ident: Id =
    IdentStr => env.from_str(<>);

CtorIdent: Id =
    Sp<Ident> => {
        if !env.string(&<>.value).starts_with(char::is_uppercase) {
            errors.push(::lalrpop_util::ParseError::User {
                error: pos::spanned(<>.span, format!("Constructors must start with an uppercase letter").into()),
            });
        }
        <>.value
    };

SpannedIdent: SpannedIdent<Id> =
    Sp<Ident> => pos::spanned(<>.span, new_ident(type_cache, <>.value));

Operator: TypedIdent<Id> =
    "operator" => new_ident(type_cache, env.from_str(<>));

DocComment: Comment =
    "documentation comment"+ => {
        let typ = <>.last().unwrap().typ;
        Comment {
            typ: typ,
            content: <>.into_iter().map(|comment| comment.content).join("\n")
        }
    };

Any: () = {
    <"identifier">,
    <"operator">,
    <"string literal">,
    <"char literal">,
    <"int literal">,
    <"byte literal">,
    <"float literal">,

    <",">,
    <"=">,

    "(" AttributeContents ")",
    "[" AttributeContents "]",
    "{" AttributeContents "}",
};

AttributeContents: () = {
    Any*
};

AttributeArguments: Option<String> = {
    "(" <start: @L> AttributeContents <end: @R> ")" =>
        Some(input.src()[(start.to_usize() - input.start_index().to_usize())..(end.to_usize() - input.start_index().to_usize())].to_string()),
    => None,
};

Attribute: Attribute = {
    "#[" <name: IdentStr> <arguments: AttributeArguments> "]" => Attribute { name: name.into(), arguments, },
};

Metadata: Metadata = {
    <comment: DocComment> => Metadata { comment: Some(comment), .. Metadata::default() },
    <comment: DocComment> <attributes: Attribute+> => Metadata { comment: Some(comment), attributes, .. Metadata::default() },
    <attributes: Attribute+> => Metadata { attributes, .. Metadata::default() },
};

// Kinds

AtomicKind: ArcKind = {
    <l: @L> <id: "identifier"> <r: @R> =>? {
        use lalrpop_util::ParseError;

        match id {
            "_" => Ok(type_cache.kind_cache.hole()),
            "Type" => Ok(type_cache.kind_cache.typ()),
            "Row" => Ok(type_cache.kind_cache.row()),
            id => Err(ParseError::User {
                error: pos::spanned2(
                    l.into(),
                    r.into(),
                    Error::UnexpectedToken(
                        "identifier".to_string(),
                        ["_", "Row", "Type"].iter().map(|s| s.to_string()).collect())),
            }),
        }
    },

    "(" <kind: Kind> ")" => kind,
};

Kind: ArcKind = {
    AtomicKind,

    <lhs: AtomicKind> "->" <rhs: Kind> =>
        Kind::function(lhs, rhs),
};

// Types

TypeParam: Generic<Id> = {
    <id : Ident> =>
        Generic::new(id, type_cache.kind_cache.hole()),

    "(" <id: Ident> ":" <kind: Kind> ")" =>
        Generic::new(id, kind),
};

RecordField: Either<Field<Id, Alias<Id, AstType<Id>>>, Field<Id, AstType<Id>>> = {
    <metadata: Metadata?> <id: Sp<Ident>> <args: Ident*> <alias: ("=" <Sp<Type_>>)> => {
        let span = id.span;
        Either::Left(Field::new(
            id.value.clone(),
            Alias::from(AliasData::new(
                id.value,
                args.into_iter()
                    .map(|id| Generic::new(id, type_cache.kind_cache.hole()))
                    .collect(),
                AstType::with_metadata(metadata, alias),
            )),
        ))
    },
    <metadata: Metadata?> <id: Sp<Ident>> => {
        let span = id.span;
        Either::Left(Field::new(
            id.value.clone(),
            Alias::new(
                id.value,
                Vec::new(),
                AstType::with_metadata(metadata, pos::spanned(span, Type::Hole)),
            ),
        ))
    },
    <metadata: Metadata?> <id: Ident> ":" <typ: Sp<Type_>> => {
        if env.string(&id).starts_with(char::is_uppercase) {
            errors.push(::lalrpop_util::ParseError::User {
                error: pos::spanned(typ.span, format!("Defining a kind for a type in this location is not supported yet").into()),
            });
            Either::Left(Field::new(
                id.clone(),
                Alias::new(
                    id,
                    Vec::new(),
                    AstType::with_metadata(metadata, typ),
                ),
            ))
        } else {
            Either::Right(Field::new(
                id,
                AstType::with_metadata(metadata, typ),
            ))
        }
    },
};

VariantField: Variant<Id> = {
    "|" <CtorIdent> <AtomicType*> => Variant::Simple(<>),
    "|" <CtorIdent> ":" <Type> => Variant::Gadt(<>)
};

VariantType: (Vec<Id>, Vec<Variant<Id>>, Option<AstType<Id>>) = {

    <row: VariantField+> <rest: (".." <AtomicType>)?> => {
        (Vec::new(), row, rest)
    },

    "forall" <args: Ident+> "." "(" <mut ty: VariantType> ")" => {
        ty.0.extend(args);
        ty
    }
};

TypeTop: Either<AstType<Id>, Spanned<(Vec<Id>, Vec<Variant<Id>>, Option<AstType<Id>>), BytePos>> = {
    Type => Either::Left(<>),
    <rest: Sp<(".." <AtomicType>)>> => {
        let Spanned { span, value: rest } = rest;
        Either::Left(
            pos::spanned(span, Type::Variant(pos::spanned(span, Type::ExtendRow { fields: Vec::new(), rest }).into())).into()
        )
    },
    <Sp<VariantType>> => Either::Right(<>),
};

TypeBinding: TypeBinding<Id> = {
    <metadata: Metadata?> "type" <type_name: Sp<Ident>> <params: TypeParam*> "=" <body: TypeTop> => {
        if !env.string(&type_name.value).starts_with(char::is_uppercase) {
            errors.push(::lalrpop_util::ParseError::User {
                error: pos::spanned(type_name.span, format!("A type must start with an uppercase letter").into()),
            });
        }

        let body = match body {
            Either::Left(body) => body,
            Either::Right(Spanned { span, value: (forall_params, variants, rest) }) => {
                let rest: AstType<_> = rest.unwrap_or_else(|| pos::spanned(span, Type::EmptyRow).into());
                let row = if variants.is_empty() {
                    rest
                } else {
                    let fields = variants.into_iter()
                        .map(|variant| match variant {
                            Variant::Gadt(name, mut typ) => {
                                {
                                    let mut typ = &mut typ;
                                    while let Type::Function(arg_type, _, ret) = &mut **typ {
                                        *arg_type = ArgType::Constructor;
                                        typ = ret;
                                    }
                                }
                                Field::new(name, typ)
                            }
                            Variant::Simple(name, types) => Field::new(
                                name,
                                Type::function_type(
                                    ArgType::Constructor,
                                    types,
                                    Type::opaque(),
                                )
                            ),
                        })
                        .collect();
                    AstType::from(pos::spanned(span, Type::ExtendRow { fields, rest }))
                };

                AstType::from(Type::forall(
                    forall_params.into_iter().map(|id| Generic::new(id, type_cache.kind_cache.hole())).collect(),
                    AstType::from(pos::spanned(span, Type::Variant(row)))
                ))
            }
        };

        TypeBinding {
            metadata: metadata.unwrap_or_default(),
            name: type_name.clone(),
            alias: pos::spanned(body.span(), AliasData::new(type_name.value.clone(), params, body)),
            finalized_alias: None,
        }
    },
};

Effect: Field<Id, AstType<Id>> = {
    <name: Ident> ":" <typ: Type> => Field { <> }
};

AtomicType_: Type<Id, AstType<Id>> = {
    "(" "->" ")" =>
        Type::Builtin(BuiltinType::Function),

    <ids: (<Ident> ".")*> <last: IdentStr> => {
        if ids.is_empty() {
            if last == "_" {
                Type::Hole
            } else {
                match BuiltinType::from_str(last) {
                    Ok(ty) => Type::Builtin(ty),
                    Err(_) if last.starts_with(char::is_uppercase) => {
                        Type::Ident(env.from_str(last))
                    }
                    Err(_) => {
                        Type::Generic(Generic::new(env.from_str(last), type_cache.kind_cache.hole()))
                    }
                }
            }
        } else {
            let ids = ids
                .iter()
                .cloned()
                .chain(Some(env.from_str(last)))
                .collect();
            Type::Projection(ids)
        }
    },

    "(" <rest: Sp<(".." <AtomicType>)>> ")" => {
        let Spanned { span, value: rest } = rest;
        Type::Variant(pos::spanned(span, Type::ExtendRow { fields: Vec::new(), rest }).into())
    },


    "[" "|" <fields: Sep<Effect, ",">> <rest: ("|" <Type>)?> "|" "]" => {
        let rest = rest.unwrap_or_else(Type::empty_row);
        Type::Effect(Type::extend_row(fields, rest))
    },

    "(" <elems: Comma<Type>> ")" =>
        match elems.len() {
            // Parenthesized type
            1 => elems.into_iter().next().unwrap().into_inner(),
            _ => Type::tuple_(env, elems),
        },

    "{" <row: Comma<RecordField>> <rest: ("|" <Type>)?> "}" => {
        let (types, fields) = row.into_iter().partition_map(|x| x);
        Type::Record(Type::extend_full_row(
            types,
            fields,
            rest.unwrap_or_else(Type::empty_row),
        ))
    },
};

AtomicType : AstType<Id> = {
    <typ: Sp<AtomicType_>> => AstType::from(typ),
};

AppType_ = {
    AtomicType_,

    <ty: AtomicType> <args: AtomicType+> =>
        Type::App(ty, args.into_iter().collect()),
};

AppType : AstType<Id> = {
    <typ: Sp<AppType_>> => AstType::from(typ),
};

Type_ = {
    AppType_,
    "forall" <args: Ident+> "." <ty: Type> =>
        Type::Forall(args.into_iter()
                         .map(|id| Generic::new(id, type_cache.kind_cache.hole()))
                         .collect(),
                     ty),

    <lhs: ArgType> <f: Sp<"->">> <rhs: Type> =>
        Type::Function(
            lhs.0,
            lhs.1,
            rhs,
        )
};

Type: AstType<Id> = {
    <typ: Sp<Type_>> => AstType::from(typ),
};

ArgType: (ArgType, AstType<Id>) = {
    <AppType> => (ArgType::Explicit, <>),
    <l: @L> "[" <typ: Type_> "]" <r: @R> =>
        (ArgType::Implicit, AstType::from(pos::spanned2(l, r, typ))),
};

// Patterns

FieldPattern : FieldPattern<'ast, Id> = {
    <id: Sp<Ident>> "=" <body: Sp<Pattern>> =>
        FieldPattern::Value(id, Some(body)),

    <Sp<IdentStr>> => {
        let id = pos::spanned(<>.span, env.from_str(<>.value));
        if <>.value.starts_with(char::is_uppercase) {
            FieldPattern::Type(id, None)
        } else {
            FieldPattern::Value(id, None)
        }
    },
};

AtomicPattern: Pattern<'ast, Id> = {
    <l: @L> <id: Ident> <r: @R> "@" <pat: Sp<AtomicPattern>> =>
        Pattern::As(pos::spanned2(l, r, id), arena.alloc(pat)),

    <id: Ident> =>
        if env.string(&id).starts_with(char::is_uppercase) {
            Pattern::Constructor(new_ident(type_cache, id), &mut [])
        } else {
            Pattern::Ident(new_ident(type_cache, id))
        },

    <l: Literal> =>
        Pattern::Literal(l),

    "(" <elems: CommaSlice<Sp<Pattern>>> ")" =>
        match elems {
            // Parenthesized pattern
            [e] => mem::take(&mut e.value),
            _ => Pattern::Tuple { typ: type_cache.hole(), elems },
        },

    "{" <fields: Comma<FieldPattern>> <implicit_import: Sp<"?"?>> "}" => {
        let mut types_len = 0;
        let mut values_len = 0;
        for field in &fields {
            match field {
                FieldPattern::Type(..) => types_len += 1,
                FieldPattern::Value(..) => values_len += 1,
            }
        }

        let mut types = mem::take(temp_vecs.select());
        types.reserve(types_len);
        let mut values = mem::take(temp_vecs.select());
        values.reserve(values_len);

        for field in fields {
            match field {
                FieldPattern::Type(id, typ) => types.push(PatternField {
                    name: id,
                    value: typ
                }),
                FieldPattern::Value(id, field) => values.push(PatternField {
                    name: id,
                    value: field
                }),
            }
        }

        let implicit_import_span = implicit_import.span;

        let pattern = Pattern::Record {
            typ: type_cache.hole(),
            types: arena.alloc_extend(types.drain(..)),
            fields: arena.alloc_extend(values.drain(..)),
            implicit_import: implicit_import
                .value
                .map(|_| pos::spanned(implicit_import_span, env.from_str(&format!("implicit?{}", implicit_import_span.start())))),
        };
        *temp_vecs.select() = types;
        *temp_vecs.select() = values;
        pattern
    },
};

NoErrorPattern = {
    AtomicPattern,

    <id: Ident> <args: Many1<Sp<AtomicPattern>>> => {
        let id = new_ident(type_cache, id);

        Pattern::Constructor(id, args)
    },
};

Pattern = {
    NoErrorPattern,
    RecoverError => {
        debug!("Recovering from pattern error");
        Pattern::Error
    },
};

// Expressions

Literal: Literal = {
    "string literal" => Literal::String(<>.unescape()),
    "char literal" => Literal::Char(<>),
    "int literal" => Literal::Int(<>),
    "byte literal" => Literal::Byte(<>),
    "float literal" => Literal::Float(<>),
};

Alternative: Alternative<'ast, Id> = {
    "|" <pat: Sp<Pattern>> "->" <expr: Sp<BlockExpr>> => {
        Alternative {
            pattern: pat,
            expr: super::shrink_hidden_spans(expr),
        }
    },
    "|" <pat: Sp<NoErrorPattern>> RecoverError <end: @R>  => {
        let span = pos::Span::new(pat.span.end(), end);
        Alternative {
            pattern: pat,
            expr: pos::spanned(span, Expr::Error(None)),
        }
    },
    "|" <start: @R> RecoverError <end: @R>  => {
        let span = pos::Span::new(start, end);
        Alternative {
            pattern: pos::spanned(span, Pattern::Error),
            expr: pos::spanned(span, Expr::Error(None)),
        }
    },
};

FieldExpr: FieldExpr<'ast, Id> = {
    <metadata: Metadata?> <id: Sp<Ident>> "=" <body: SpExpr> => {
        FieldExpr::Value(metadata.unwrap_or_default(), id, Some(body))
    },

    <metadata: Metadata?> <id_str: Sp<IdentStr>> => {
        let metadata = metadata.unwrap_or_default();
        let id = pos::spanned(id_str.span, env.from_str(id_str.value));
        if id_str.value.starts_with(char::is_uppercase) {
            FieldExpr::Type(metadata, id, None)
        } else {
            FieldExpr::Value(metadata, id, None)
        }
    },
};

ValueArgument: Argument<SpannedIdent<Id>> = {
    <arg_type: "?"?> <name: SpannedIdent> => {
        Argument {
            arg_type: if arg_type.is_some() {
                ArgType::Implicit
            } else {
                ArgType::Explicit
            },
            name,
        }
    }
};

ValueBinding: ValueBinding<'ast, Id> = {
    <metadata: Metadata?> <mut bind: PlainValueBinding> => {
        bind.metadata = metadata.unwrap_or_default();
        bind
    }
};

PlainValueBinding: ValueBinding<'ast, Id> = {
    "let" <name: Sp<AtomicPattern>> <typ: (":" <Type>)?> "=" <expr: SpExpr> => {
        ValueBinding {
            metadata: Metadata::default(),
            name,
            typ,
            resolved_type: type_cache.hole(),
            args: Default::default(),
            expr,
        }
    },

    "let" <name: Sp<Ident>> <args: Many1<ValueArgument>> <typ: (":" <Type>)?> "=" <expr: SpExpr> =>
        ValueBinding {
            metadata: Metadata::default(),
            name: name.map(|name| new_ident(type_cache, name)).map(Pattern::Ident),
            typ,
            resolved_type: type_cache.hole(),
            args,
            expr,
        },

    // Error recovery

    "let" <name: Sp<Ident>> => {
        let span = pos::Span::new(name.span.end(), name.span.end());
        errors.push(::lalrpop_util::ParseError::UnrecognizedToken {
            token: (span.start(), Token::In, span.end()),
            expected: ["=", ":"].iter().map(|s| s.to_string()).collect(),
        });
        ValueBinding {
            metadata: Metadata::default(),
            name: name.map(|name| new_ident(type_cache, name)).map(Pattern::Ident),
            typ: None,
            resolved_type: type_cache.hole(),
            args: Default::default(),
            expr: pos::spanned(span, Expr::Error(None)),
        }
    }
};

RecursiveValueBinding: ValueBinding<'ast, Id> = {
    <metadata: Metadata?> "let" <name: Sp<Ident>> <args: Many<ValueArgument>> <typ: (":" <Type>)?> "=" <expr: SpExpr> =>
        ValueBinding {
            metadata: metadata.unwrap_or_default(),
            name: name.map(|name| new_ident(type_cache, name)).map(Pattern::Ident),
            typ,
            resolved_type: type_cache.hole(),
            args,
            expr,
        },
};

RecordExprBase: Option<SpannedExpr<'ast, Id>> = {
    ".." <SpExpr> => Some(<>),
    => None
};

AtomicExpr: Expr<'ast, Id> = {
    <id: Ident> =>
        Expr::Ident(new_ident(type_cache, id)),

    <lit: Literal> =>
        Expr::Literal(lit),

    // TODO: Getters
    // "(" "." <id: Ident> ")" =>
    //     Expr::Getter(id),

    <expr: SpAtomicExpr> "." <id: Ident> =>
        Expr::Projection(arena.alloc(expr), id, type_cache.hole()),

    <expr: SpAtomicExpr> "." RecoverError => {
        Expr::Projection(arena.alloc(expr), env.from_str(""), type_cache.hole())
    },

    "(" <elems: CommaSlice<SpExpr>> ")" =>
        Expr::Tuple { typ: type_cache.hole(), elems },

    "[" <exprs: CommaSlice<SpExpr>> "]" => Expr::Array(Array {
            typ: type_cache.hole(),
            exprs,
        }),

    "{" <fields: CommaTemp<FieldExpr>> <base: RecordExprBase> "}" => {
        let mut types = mem::take(temp_vecs.select());
        let mut values = mem::take(temp_vecs.select());

        for field in temp_vecs.drain(fields) {
            match field {
                FieldExpr::Type(metadata, id, typ) => types.push(ExprField {
                    metadata,
                    name: id,
                    value: typ
                }),
                FieldExpr::Value(metadata, id, expr) => values.push(ExprField {
                    metadata,
                    name: id,
                    value: expr
                }),
            }
        }

        let expr = Expr::Record {
            typ: type_cache.hole(),
            types: arena.alloc_extend(types.drain(..)),
            exprs: arena.alloc_extend(values.drain(..)),
            base: base.map(|e| arena.alloc(e)),
        };
        *temp_vecs.select() = types;
        *temp_vecs.select() = values;
        expr
    },
};

SpAtomicExpr: SpannedExpr<'ast, Id> = {
    <Sp<AtomicExpr>> => super::shrink_hidden_spans(<>)
};

ImplicitArg: SpannedExpr<'ast, Id> = {
    "?" <arg: Sp<AtomicExpr>> => arg
};

AppExpr = {
    AtomicExpr,

    <expr: SpAtomicExpr> <implicit_args: Many1<ImplicitArg>> =>
        Expr::App { func: arena.alloc(expr), implicit_args, args: &mut [] },

    <expr: SpAtomicExpr> <implicit_args: Many<ImplicitArg>> <args: Many1<SpAtomicExpr>> =>
        Expr::App { func: arena.alloc(expr), implicit_args, args },
};


LambdaArgument: Argument<SpannedIdent<Id>> = {
    <name: SpannedIdent> => {
        Argument::explicit(name)
    }
};

InfixExpr = {
    AppExpr,

    "\\" <args: Many1<LambdaArgument>> "->" <body: SpExpr> =>
        Expr::Lambda(Lambda {
            id: new_ident(type_cache, env.from_str("")),
            args,
            body: arena.alloc(body),
        }),

    <lhs: Sp<AppExpr>> <op: Sp<Operator>> <rhs: Sp<InfixExpr>> =>
        Expr::Infix { lhs: arena.alloc(lhs), op, rhs: arena.alloc(super::shrink_hidden_spans(rhs)), implicit_args: &mut [], },
};


InExpr: SpannedExpr<'ast, Id> = {
    "in" <SpExpr>,
    <err: Sp<RecoverError>> => {
        pos::spanned(err.span, Expr::Error(None))
    }
};

Expr: Expr<'ast, Id> = {
    InfixExpr,

    "if" <pred: SpExpr> "then" <if_true: SpExpr> "else" <if_false: SpExpr> =>
        Expr::IfElse(arena.alloc(pred), arena.alloc(if_true), arena.alloc(if_false)),

    "match" <body: SpExpr> "with" <arms: Many1<Alternative>> =>
        Expr::Match(arena.alloc(body), arms),

    <binding: ValueBinding> <body: InExpr> => {
        Expr::LetBindings(ValueBindings::Plain(arena.alloc(binding)), arena.alloc(body))
    },

    <binding: TypeBinding> <body: InExpr> => {
        Expr::TypeBindings(arena.alloc_extend(Some(binding)), arena.alloc(body))
    },

    <metadata: Metadata?> "rec" <mut bindings: Many1<RecursiveValueBinding>> <body: InExpr> => {
        if let Some(metadata) = metadata {
            bindings[0].metadata = metadata;
        }
        Expr::LetBindings(ValueBindings::Recursive(bindings), arena.alloc(body))
    },

    <metadata: Metadata?> "rec" <mut bindings: Many1<TypeBinding>> <body: InExpr> => {
        if let Some(metadata) = metadata {
            bindings[0].metadata = metadata;
        }
        Expr::TypeBindings(bindings, arena.alloc(body))
    },

    DoExpression,

    BlockExpr,

    RecoverError => {
        Expr::Error(None)
    }
};

DoExpression: Expr<'ast, Id> = {
    "do" <bind: DoBinding> <body: SpExpr> => {
        let bind = *bind;
        Expr::Do(arena.alloc(Do { id: Some(bind.0), bound: arena.alloc(bind.1), body: arena.alloc(body), flat_map_id: None }))
    },

    "seq" <bound: SpExpr> <body: InExpr> => {
        Expr::Do(arena.alloc(Do { id: None, bound: arena.alloc(bound), body: arena.alloc(body), flat_map_id: None }))
    },
};


DoBinding: Box<(SpannedPattern<'ast, Id>, SpannedExpr<'ast, Id>)> = {
    <id: Sp<Pattern>> "=" <bound: SpExpr> "in" => Box::new((id, bound)),

    // Error recovery

    <id: Sp<Pattern>> "in" => {
        let span = pos::Span::new(id.span.end(), id.span.end());
        errors.push(::lalrpop_util::ParseError::UnrecognizedToken {
            token: (span.start(), Token::In, span.end()),
            expected: ["="].iter().map(|s| s.to_string()).collect(),
        });
        Box::new((id, pos::spanned(span, Expr::Error(None))))
    },
};

BlockExpr: Expr<'ast, Id> = {
    "block open" <exprs: SepSlice1<SpExpr, "block separator">> "block close" => {
        Expr::Block(exprs)
    },
};

SpExpr: SpannedExpr<'ast, Id> = {
    <expr: Sp<Expr>> => super::shrink_hidden_spans(expr),
};

pub TopExpr: SpannedExpr<'ast, Id> = {
    "shebang line"? <expr: SpExpr> SkipExtraTokens => expr,
};

pub ReplLine: Option<Box<ReplLine<'ast, Id>>> = {
    <TopExpr> => Some(Box::new(ReplLine::Expr(<>))),
    "block open" <PlainValueBinding> "block close" => Some(Box::new(ReplLine::Let(<>))),
    => None,
};
