let prelude @ { Eq, Show } = import! std.prelude
let { (<|) } = import! std.function
let { Test, run, assert, assert_eq, assert_neq, test, group, ? } = import! std.test
let { Applicative, (*>) } = import! std.applicative

#[derive(Eq, Show)]
type TestVariant =
    | A Int
    | B String Bool

#[derive(Eq, Show)]
type TestRecord = { x : Int, name : String }


#[derive(Eq, Show)]
type Nested = { variant : TestVariant, record : TestRecord }

#[derive(Eq)]
type Recursive = | End | Rec Recursive

let eq_tests =
    let variant =
        [
            test "neq" <| \_ ->
                assert_neq (A 2) (A 1),
            test "neq" <| \_ ->
                assert_neq (A 2) (B "" True),
            test "eq" <| \_ ->
                assert_eq (A 2) (A 2),
        ]

    let record =
        [
            test "neq" <| \_ ->
                assert_neq { x = 1, name = "a" } { x = 1, name = "" },
            test "eq" <| \_ ->
                assert_eq { x = 1, name = "a" } { x = 1, name = "a" },
        ]

    let nested =
        let a = { variant = A 1, record = { x = 1, name = "" } }
        [
            test "neq" <| \_ ->
                assert_neq a { variant = A 2, .. a },
            test "eq" <| \_ ->
                assert_eq a a,
        ]

    let recursive =
        let a = Rec (Rec End)
        [
            test "neq" <| \_ ->
                assert_neq (Rec End) End,
            test "eq" <| \_ ->
                assert_eq a a,
        ]

    [
        group "variant" variant,
        group "record" record,
        group "nested" nested,
        group "recursive" recursive,
    ]

let show_tests =
    let record = { x = 123, name = "abc" }
    [
        test "variant_a" <| \_ -> assert_eq (show (A 2)) "A (2)",
        test "variant_b" <| \_ -> assert_eq (show (B "test" False)) "B (\"test\") (False)",
        test "record" <| \_ -> assert_eq (show record) "{ x = 123, name = \"abc\" }",
        test "nested" <| \_ -> assert_eq (show { variant = A 100, record }) "{ variant = A (100), record = { x = 123, name = \"abc\" } }",
    ]

group "derive" [
    group "show" show_tests,
    group "eq" eq_tests,
]
