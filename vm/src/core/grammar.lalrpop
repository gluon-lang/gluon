use base::ast::{Literal, TypedIdent};
use base::pos::{BytePos, ExpansionId, Span};
use base::symbol::{Symbol, Symbols};
use base::types::{Field, Type};

use core::{Allocator, Alternative, Closure, Expr, LetBinding, Named, Pattern};

grammar<'env, 'a>(symbols: &'env mut Symbols, allocator: &'a Allocator<'a>);

Comma<Rule>: Vec<Rule> =
    <rules: (<Rule> ",")*> <last: Rule?> => {
        let mut rules = rules;
        rules.extend(last);
        rules
    };


Identifier: Symbol = {
    <r"[A-Za-z_][A-Za-z0-9_]*"> => symbols.symbol(<>),
    <r"\(#?[A-Za-z_]*[+\-*/]\)"> => symbols.symbol(&<>[1..<>.len() - 1])
};

Field: (Symbol, Option<Symbol>) = {
    <field: Identifier> <binding: ("=" <Identifier>)?> => (field, binding)
};

Literal: Literal = {
    <r"[0-9]+"> => Literal::Int(<>.parse().unwrap()),
    <r#""[^"]*""#> => Literal::String(<>[1..<>.len() - 1].to_string()),
};

Pattern: Pattern = {
    <id: Identifier> => {
        if id.as_ref().starts_with(char::is_uppercase) {
            Pattern::Constructor(TypedIdent::new(id), Vec::new())
        } else {
            Pattern::Ident(TypedIdent::new(id))
        }
    },
    <id: Identifier> <args: Identifier+> =>
        Pattern::Constructor(TypedIdent::new(id), args.into_iter().map(TypedIdent::new).collect()),
    "{" <Comma<Field>> "}" => Pattern::Record(<>.into_iter()
        .map(|(field, binding)| (TypedIdent::new(field), binding))
        .collect()),
    <Literal> => Pattern::Literal(<>),
};

Alternative: Alternative<'a> = {
    "|" <pattern: Pattern> "->" <expr: AllocExpr> => Alternative {
        pattern: pattern,
        expr: expr,
    },
};

FieldExpr : (Symbol, Option<Expr<'a>>) = {
    <Identifier> <("=" <Expr>)?>,
};

AtomicExpr: Expr<'a> = {
    "(" <Expr> ")",
    "{" <args: Comma<FieldExpr>> "}" => {
        let id = TypedIdent {
            name: symbols.symbol("<record>"),
            typ: Type::record(vec![], args.iter()
                .map(|&(ref arg, _)| Field { name: arg.clone(), typ: Type::hole(), })
                .collect()),
        };
        let args = args.into_iter()
            .map(|(id, expr)| expr.unwrap_or_else(|| Expr::Ident(TypedIdent::new(id), Span::default())));
        let args = allocator.arena.alloc_extend(args);
        Expr::Data(id, args, BytePos::default(), ExpansionId::default())
    },
    <id: Identifier> => {
        if id.as_ref().starts_with(char::is_uppercase) {
            Expr::Data(TypedIdent::new(id), &[], BytePos::default(), ExpansionId::default())
        } else {
            Expr::Ident(TypedIdent::new(id), Span::default())
        }
    },
    <Literal> => Expr::Const(<>, Span::default()),
};

AllocExpr: &'a Expr<'a> = {
    <Expr> => allocator.arena.alloc(<>)
};

LetBinding: LetBinding<'a> = {
    <name: Identifier> "=" <expr: AllocExpr> => LetBinding {
        name: TypedIdent::new(name),
        expr: Named::Expr(expr),
        span_start: BytePos::default(),
    },
    <name: Identifier> <args: Identifier+> "=" <expr: AllocExpr> => LetBinding {
        name: TypedIdent::new(name.clone()),
        expr: Named::Recursive(vec![Closure {
            pos: BytePos::default(),
            name: TypedIdent::new(name),
            args: args.into_iter().map(TypedIdent::new).collect(),
            expr,
        }]),
        span_start: BytePos::default(),
    }
};

pub Expr: Expr<'a> = {
    AtomicExpr,
    <f: AtomicExpr> <args: AtomicExpr+> => {
        let args = allocator.arena.alloc_extend(args.into_iter());
        match f {
            Expr::Data(id, ..) =>
                Expr::Data(id, args, BytePos::default(), ExpansionId::default()),
            f => Expr::Call(allocator.arena.alloc(f), args)
        }
    },
    "let" <bind: LetBinding> "in" <expr: AllocExpr> => {
        Expr::Let(bind, expr)
    },
    "match" <expr: AllocExpr> "with" <alts: Alternative+> "end" =>
        Expr::Match(expr, allocator.alternative_arena.alloc_extend(alts.into_iter())),
};
