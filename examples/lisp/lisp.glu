let prelude = import! std.prelude
let { Functor, Applicative, Monad } = prelude
let { Foldable } = import! std.foldable
let { id } = import! std.function

let string = import! std.string
let { (<>) } = import! std.prelude

let { Bool } = import! std.bool
let float = import! std.float
let int = import! std.int
let result @ { Result, ? } = import! std.result
let { Option } = import! std.option

let list @ { List, ? } = import! std.list

let map @ { Map, ? } = import! std.map

let { Expr, Function, Lisp, LispState } = import! "examples/lisp/types.glu"
let lisp_parser = import! "examples/lisp/parser.glu"
let parser = import! std.parser

let eq : Eq Expr =
    let eq_expr l r : Expr -> Expr -> Bool =
        match (l, r) with
        | (Atom ls, Atom rs) -> ls == rs
        | (Int ls, Int rs) -> ls == rs
        | (Float ls, Float rs) -> ls == rs
        | (List ls, List rs) ->
            let list_eq : Eq (List Expr) = list.eq ?{ (==) = eq_expr }
            list_eq.(==) ls rs
        | _ -> False
    { (==) = eq_expr }

let show_expr : Show Expr =
    let spaced show_ xs =
        match xs with
        | Cons y ys -> show_ y <> spaced1 show_ ys
        | Nil -> ""
    and spaced1 show_ xs =
        match xs with
        | Cons y ys -> " " <> show_ y <> spaced1 show_ ys
        | Nil -> ""
    let show expr =
        match expr with
        | Atom s -> s
        | Int i -> int.show.show i
        | Float f -> float.show.show f
        | List ls -> "(" <> spaced show ls <> ")"
        | Function f ->
            let vararg =
                match f.vararg with
                | Some arg -> " . " <> arg
                | None -> ""
            "(lambda (" <> spaced id f.params <> ")" <> vararg
                <> ") ...)"
        | Primitive _ -> "<primitive>"
    { show }

let lisp : Lisp a -> Lisp a = id

let functor_Lisp : Functor Lisp = { map = \f m -> lisp (\state ->
            match lisp m state with
            | Ok r -> Ok { value = f r.value, state = r.state }
            | Err err -> Err err) }

let applicative_Lisp : Applicative Lisp = {
    functor = functor_Lisp,
    apply = \f m -> lisp (\state ->
            match lisp f state with
            | Ok g ->
                match lisp m g.state with
                | Ok r -> Ok { value = g.value r.value, state = r.state }
                | Err err -> Err err
            | Err err -> Err err),
    wrap = \value -> lisp (\state -> Ok { value, state }),
}

let monad_Lisp : Monad Lisp = {
    applicative = applicative_Lisp,
    flat_map = \f m -> lisp (\state ->
            match lisp m state with
            | Ok r -> lisp (f r.value) r.state
            | Err err -> Err err),
}

let { wrap } = import! std.applicative
let { flat_map, (>>=) } = import! std.monad
let { fold_m } = import! std.foldable
let traverse = list.traversable.traverse applicative_Lisp
let { map = map_lisp } = functor_Lisp

let get_state : Lisp LispState = lisp (\state -> Ok { value = state, state = state })
let set_state state : LispState -> Lisp () = lisp (\_ -> Ok { value = (), state })
let modify_state f : (LispState -> LispState) -> Lisp () =
    do state = get_state
    set_state (f state)
let scope_state run : Lisp a -> Lisp a =
    do original = get_state
    do x = run
    do set_state original
    wrap x

let fail msg : String -> Lisp a = lisp (\_ -> Err msg)

let primitive name f : String -> _ -> Map String Expr = map.singleton name (Primitive f)

type Binop a = a -> a -> a

let primitive_binop name int_op float_op : _ -> Binop Int -> Binop Float -> Map String Expr =
    let unpack_int x : Expr -> Lisp Int =
        match x with
        | Int i -> wrap i
        | _ -> fail "Expected integer"
    let unpack_float x : Expr -> Lisp Float =
        match x with
        | Float f -> wrap f
        | _ -> fail "Expected float"

    let fold unpack op : (Expr -> Lisp a)
            -> Binop a
            -> a
            -> List Expr
            -> _
        = fold_m (\acc x -> map_lisp (\y -> op acc y) (unpack x))

    let f xs : List Expr -> Lisp Expr =
        match xs with
        | Cons l ys ->
            match l with
            | Int li -> map_lisp Int (fold unpack_int int_op li ys)
            | Float lf -> map_lisp Float (fold unpack_float float_op lf ys)
            | _ -> fail ("Cant add " <> show l)
        | _ -> fail ("Expected two arguments to binop, got " <> show (List xs))
    primitive name f

let define xs =
    match xs with
    | Cons (Atom name) (Cons value Nil) ->
        do state = get_state
        let new_state = map.insert name value state
        do set_state new_state
        wrap value
    | Cons (List (Cons (Atom name) params)) body ->
        do closure = get_state

        let function = Function {
                    params = list.functor.map show params,
                    vararg = None,
                    body,
                    closure,
                }
        let new_state = map.insert name function closure

        do set_state new_state

        wrap function
    | _ -> fail "Unexpected parameters to define `define`"

let primitives : LispState =
    let { (<>) = (++) } = import! std.prelude
    primitive_binop "+" (+) (+) ++ primitive_binop "-" (-) (-) ++ primitive_binop "*" (*) (*)
        ++ primitive_binop "/" (/) (/)
        ++ primitive "define" define

let apply f xs : Expr -> List Expr -> Lisp Expr =
    let add_args names values =
        match (names, values) with
        | (Cons name names, Cons value values) ->
            do modify_state (\state -> map.insert name value state)
            add_args names values
        | (Nil, _) -> wrap ()
        | _ -> fail "Not enough arguments to function"

    match f with
    | Primitive primitive -> primitive xs
    | Function function ->
        scope_state (
            do add_args function.params xs
            eval_exprs function.body)
    | _ -> fail ("Can\'t call value: " <> show f)

and eval_lisp expr : Expr -> Lisp Expr =
    match expr with
    | Atom name ->
        do state = get_state
        match map.find name state with
        | Some value -> wrap value
        | None -> fail ("Binding `" <> name <> "` is not defined")
    | Int _ -> wrap expr
    | Float _ -> wrap expr
    | Function _ -> wrap expr
    | List list ->
        match list with
        | Cons x xs ->
            match x with
            | Atom name ->
                do state = get_state
                if name == "define" then
                    define xs
                else
                    match map.find name state with
                    | Some prim ->
                        do evaluated_args = traverse eval_lisp xs
                        apply prim evaluated_args
                    | None -> fail ("Variable `" <> name <> "` does not exist")
            | _ -> fail ("Cant apply " <> show x)
        | Nil -> wrap expr

and eval_exprs exprs = fold_m (\_result expr -> eval_lisp expr) (List Nil) exprs

let eval_env expr env : Lisp a -> Map String Expr -> Result String a =
    result.functor.map (\r -> r.value) (lisp expr env)

let eval expr : Expr -> Result String Expr = eval_env (eval_lisp expr) primitives
let eval_seq exprs =
    match exprs with
    | Cons _ _ -> eval_env (eval_exprs exprs) primitives
    | Nil -> error "Expected at least one lisp expression"

let eval_string s = parser.parse lisp_parser.expr s >>= eval
let eval_env_string s env =
    do e = parser.parse lisp_parser.expr s
    do l = lisp (eval_lisp e) env
    wrap (l.value, l.state)

{
    LispState,
    Expr,
    eq,
    show = show_expr,
    expr = lisp_parser.expr,

    default_env = primitives,

    eval,
    eval_seq,
    eval_string,
    eval_env_string,
}
