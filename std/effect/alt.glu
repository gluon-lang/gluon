let { Eff, inject_rest, ? } = import! std.effect
let { map } = import! std.functor
let { wrap } = import! std.applicative
let { Alternative } = import! std.alternative
let { (<<), id } = import! std.function

type Alt a = forall r . (| Empty .. r)

let extract_alt x : forall s . [| alt : Alt | r |] a -> Alt a = convert_variant! x

let send_alt f : Alt a -> Eff [| alt : Alt | r |] a = Impure (convert_effect! alt f) Pure

let empty : forall s . Eff [| alt : Alt | r |] s =
    send_alt Empty

let run_alt_inner transform fail eff_1 eff_2 : (a -> b) -> (() -> Eff [| | s |] b) -> Eff [| alt : Alt | r |] a -> Eff [| alt : Alt | r |] a -> Eff [| | s |] b =
    let loop next ve =
        match ve with
        | Pure value -> wrap (transform value)
        | Impure e f ->
            match extract_alt e with 
            | Empty ->
                next ()
            | rest ->
                Impure (inject_rest rest) (loop next << f)
    let loop_2 _ = loop fail eff_2
    loop loop_2 eff_1

///
/// ```
/// let { assert } = import! std.assert
/// let alt = import! std.effect.alt
/// let state = import! std.effect.state
/// let { (*>) } = import! std.applicative
/// let { Eff, run_pure, ? } = import! std.effect
///
/// let incr = state.modify (\x -> x + 1)
/// 
/// assert (run_pure (state.exec_state 0 (alt.run_alt incr incr)) == 1)
/// assert (run_pure (state.exec_state 0 (alt.run_alt (incr *> incr) incr)) == 2)
/// assert (run_pure (state.exec_state 0 (alt.run_alt (incr *> alt.empty *> incr) incr)) == 2)
/// assert (run_pure (state.exec_state 0 (alt.run_alt alt.empty incr)) == 1)
/// 
/// ```
let run_alt eff_1 eff_2 : Eff [| alt : Alt | r |] a -> Eff [| alt : Alt | r |] a -> Eff [| | r |] (Option a) =
    let fail _ = wrap None
    run_alt_inner Some fail eff_1 eff_2

let alternative : Alternative (Eff [| alt : Alt | r |]) = {
    applicative = (import! std.effect).applicative,
    or = \l r -> run_alt_inner id (\_ -> empty) l r,
    empty = empty
}

{
    Alt,

    alternative,

    empty,
    run_alt,
}
