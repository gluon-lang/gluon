let prelude = import! std.prelude
let { Eq, Ord, Ordering, Show } = prelude
let { Functor, Applicative, Monad, Traversable } = prelude
let { Result } = import! std.types
let { Foldable } = import! std.foldable
let { Bool } = import! std.bool
let string = import! std.string

let unwrap_ok res : Result e a -> a =
    match res with
    | Ok x -> x
    | Err _ -> error "Result was an Err"

let unwrap_err res : Result e a -> e =
    match res with
    | Ok _ -> error "Result was an Ok"
    | Err x -> x

let eq ?e ?a : [Eq e] -> [Eq a] -> Eq (Result e a) = {
    (==) = \l r ->
        match (l, r) with
        | (Ok l_val, Ok r_val) -> a.(==) l_val r_val
        | (Err l_val, Err r_val) -> e.(==) l_val r_val
        | _ -> False,
}

let ord ?e ?a : [Ord e] -> [Ord a] -> Ord (Result e a) = {
    eq = eq,
    compare = \l r ->
        match (l, r) with
        | (Ok l_val, Ok r_val) -> a.compare l_val r_val
        | (Err l_val, Err r_val) -> e.compare l_val r_val
        | (Ok _, Err _) -> LT
        | (Err _, Ok _) -> GT,
}

let functor : Functor (Result e) = {
    map = \f x ->
        match x with
        | Ok y -> Ok (f y)
        | Err e -> Err e,
}

let applicative : Applicative (Result e) = {
    functor = functor,
    apply = \f x ->
        match (f, x) with
        | (Ok g, Ok y) -> Ok (g y)
        | (Ok _, Err e) -> Err e
        | (Err x, _) -> Err x,
    wrap = \x -> Ok x,
}

let monad : Monad (Result e) = {
    applicative = applicative,
    flat_map = \f m ->
        match m with
        | Ok x -> f x
        | Err err -> Err err,
}

let foldable : Foldable (Result e) = {
    foldr = \f z r ->
        match r with
        | Err _ -> z
        | Ok x -> f x z,
    foldl = \f z r ->
        match r with
        | Err _ -> z
        | Ok x -> f z x,
}

let traversable : Traversable (Result e) = {
    functor = functor,
    foldable = foldable,
    traverse = \app f r ->
        match r with
        | Err e -> app.wrap (Err e)
        | Ok x -> app.functor.map Ok (f x),
}

let show ?e ?t : [Show e] -> [Show t] -> Show (Result e t) =
    let (++) = string.append

    let show o =
        match o with
        | Ok x -> "Ok (" ++ t.show x ++ ")"
        | Err x -> "Err (" ++ e.show x ++ ")"

    { show }

{
    Result,
    unwrap_ok,
    unwrap_err,
    eq,
    ord,
    functor,
    applicative,
    monad,
    foldable,
    traversable,
    show,
}
