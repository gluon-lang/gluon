let string  = import! "std/string.glu"
and { Writer, make = make_Writer, tell }  = import! "std/writer.glu"
and prelude  = import! "std/prelude.glu"
and { Show, Num, Eq, Option, List, Applicative, Monad, Monoid, foldl } = prelude
and { (+) } = prelude.num_Int
and { (==) } = prelude.eq_Int
and { (<) } = prelude.make_Ord prelude.ord_Int

let (++) = string.monoid.append

type Test a = Writer (List String) a

let writer = make_Writer prelude.monoid_List
let { pure } = writer.applicative

let assert x = if x then () else error "Assertion failed"
and assert_eq show eq = \x y ->
    if eq.(==) x y
    then pure ()
    else tell (Cons ("Assertion failed: "
                ++ show.show x  ++ " != " ++ show.show y) Nil)

let assert_ieq = assert_eq prelude.show_Int prelude.eq_Int
and assert_feq = assert_eq prelude.show_Float prelude.eq_Float
and assert_seq = assert_eq string.show string.eq

let run test : Test a -> () =
        match test.writer with
            | Cons _ _ -> error (prelude.foldl (\acc err -> acc ++ "\n" ++ err) "" test.writer)
            | Nil -> ()

{ Test, writer, assert, assert_eq, assert_ieq, assert_feq, assert_seq, run }
